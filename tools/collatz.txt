        *INSTR.  ARGUMENTS                      REMARKS                 SORT
        
         ORIGIN  1024

START    LX      13,STACK

         LX      0,27
         ST      0,0-
         LA      13,11
         BASM    .COLLATZ
         
         HLT

        ****************************************************************
        * COLLATZ - COMMUNIST PLOT TO WASTE MATHEMATICIANS' TIME
        *
        * AP + 00 -> NUMBER
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

COLLATZ  USING   0,2,3,11

         LN      0,0(11)                        GET ARGUMENT AS NEGATIVE
         
COL_LOOP IA      0,0,NL,TRZ                     STOP ON 1
         B       .COL_END
         
         TACBZ   0                              TEST IF ODD
         B       .COL_EVEN
         
COL_ODD  LA      0,3,CC,RC(1)                   3N+1 (3N-1) IF ODD
         AX      0,-1(3)
         TNV
         
COL_EVEN LA      0,0,SC,RC(-1)                  N/2 IF EVEN

COL_PRT  NA      0,2                            GET POSITIVE
         LX      11,.O2A_ARG                    SETUP OCTAL TO ASCII
         ST      2,0(11)                        SAVE NUMBER
         BASM    .OCT2ASCI
         
         LX      2,.COL_NBUF                    PRINT NUMBER
         LX      11,.LPT_ARG
         ST      2,1(11)
         BASM    .LPTWRITE
         
         LX      2,.COL_CMMA                    PRINT COMMA
         ST      2,1(11)
         BASM    .LPTWRITE
         
         B       .COL_LOOP                      CONTINUE

COL_END  LX      2,.COL_ENDL                    END LINE
         LX      11,.LPT_ARG
         ST      2,1(11)
         BASM    .LPTWRITE
         
         BRM                                    DONE.

COL_NBUF DW      0,0,0
COL_CMMA ASCII   ,
COL_ENDL ASCII   END\012

LPT_ARG  DW      11,0
O2A_ARG  DW      0,COL_NBUF

        ****************************************************************
        * LPTWRITE - PRINT 7-BIT ASCII TO LINE PRINTER
        *
        * AP + 00 -> PRINTER DEVICE ID
        *    + 01 -> BASE OF STRING (NULL-TERMINATED BUFFER)
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

LPTWRITE USING   0,2,3

         LX      3,-1                           GET ONE BEFORE STRING
         A       3,1(11)                        TO INIT INDEX PROPERLY
         
         L       2,0(11)                        DEVICE ID
         
LPW_LOOP EDITS   2,.1                           OVERWRITE DEVICE ID
         TIOBZ   0                              LOOP IF PRINTER BUSY
         B       .LPW_LOOP
         
         ILC     0,3,7                          GET CHARACTER
         TACZ                                   BREAK OUT ON NULL
         B       .LPW_END
         
         EDITS   2,.1                           OVERWRITE DEVICE ID
         WIOS    0,0,0                          SEND CHARACTER TO LPT
         
         B       .LPW_LOOP

LPW_END  EDITS   2,.1                           OVERWRITE DEVICE ID
         TIOBZ   0                              LOOP IF LPT STILL BUSY
         B       .LPW_END
         
         BRM                                    DONE.

        ****************************************************************
        * OCT2ASCI - GENERATE OCTAL STRING FROM WORD
        *
        * AP + 00 -> NUMBER TO CONVERT
        *    + 01 -> BASE OF STRING (NULL-TERMINATED BUFFER)
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

OCT2ASCI USING   0,2,3,4

         LX      3,-1                           GET ONE BEFORE STRING
         A       3,1(11)                        TO INIT INDEX PROPERLY
         
         L       2,0(11)                        ARGUMENT
         
         LX      0,-12                          ITERATE -12 DIGITS

O2A_LOOP R       2,3                            EXTRACT NEXT DIGIT
         LA      2,4,CC,M(33)
         LX      4,48(4)                        CONVERT TO ASCII
         
         ISTC    4,3,7                          DEPOSIT CHARACTER
         
         IA      0,0,TRNZ                       REPEAT UNTIL DONE
         B       .O2A_LOOP
         
         XA      4,4                            NULL TERMINATOR
         ISTC    4,3,7
         
         BRM                                    DONE.

        ****************************************************************

         ORIGIN  2048

STACK    BSS     0

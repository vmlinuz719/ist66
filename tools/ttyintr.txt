        *INSTR.  ARGUMENTS                      REMARKS                 SORT

         ORIGIN  0                              EXCEPTION
         
EXC_VEC  DW      EXC_STUB,0

         ORIGIN  20                             TTY HANDLER

TTY_VEC  DW      TTY_INTR,0

         ORIGIN  62                             USER SAVE

USR_SAVE DW      START,0740000000000

         ORIGIN  1024

IPL      LX      3,.USR_RST
         LX      4,.USR_SAVE
         L       0(3)
         ST      0(4)
         L       1(3)
         ST      1(4)
         LCTL    1,.CW
         RLMSK   .MASK

USR_RST  DW      START,0740000000000

EXC_STUB ST      13,.EXC_SPSV
         LX      13,.EXC_STK0
         BSM     .EXHANDLR
         RFI     0

EXHANDLR USING   0-15
         LX      13,.EXC_STK1
         
         STCTL   1,1-
         L       1+
         LA      0,3,CC,R(12),M(32)
         LX      4,.EXTAB
         AA      3,4
         L       7,0(4)
         TRN     7
         B       .ALT_HNDL
         
         LA      0,0,CC,R(8),M(32)
         LA      0,7,R(1)
         AX      7,34
         AX      3,.EXCNAMES
         L       0(3)
         ST      1-
         LX      4
         ST      1-
         
         LX      3,.EXC_PFIX
         BSM     .WRITELD
         LA      13,3
         BSM     .WRITELD
         AX      13,2
         LX      3,.PSL
         BSM     .WRITELD
         AX      13,-6                          Num buf 3(13)
         LX      9
         ST      0(13)                          Pad buf 0(13)
         
         LX      8,-2
         
DUMP_PSW AA      7,7,I(-1)
         LX      3,3(13)
         L       4,0(7)
         LX      5,16
         BSM     .NUM2LD

         LX      3,3(13)
         LX      4,0(13)
         LX      5,060
         BSM     .LEFT_PAD

         LX      3,0(13)
         BSM     .WRITELD
         
         IA      8,8,TRN
         B       .DUMP_PSW
         
         AX      13,6
         LX      3,CRLF
         BSM     .WRITELD
         LX      3,CRLF
         BSM     .WRITELD
         
         L       .EXC_SPSV
         ST      .EXC_OLDS
         
         LX      3,.EXC_SAVE
         BSM     .DUMPREGS
         
         LCTL    1,.HALT
         LMSK    .HALT
         HLT
         
ALT_HNDL LX      3,.EXC_SAVE
         L       .EXC_SPSV
         ST      .EXC_OLDS
         BSM     0(7)
         
         LX      13,.EXC_TOP
         TACN
         B       .NO_RETRY
         BRM

NO_RETRY LX      .RFI_1
         ST      0(13)
         BRM
RFI_1    RFI     1

HALT     DW      0

EXC_SPSV BSS     1
EXC_TOP  BSS     2
EXC_SAVE BSS     13
EXC_OLDS BSS     3
EXC_STK0 BSS     0
         BSS     128
EXC_STK1 BSS     0

EXTAB    BSS     16   

CW       DW      0036000000000                  RETURN TO IRQL 15
MASK     DW      02000                          ENABLE CONSOLE IRQ
TTY_STAT DW      0

TTY_INTR NIOC    48                             CLEAR INTR
         ST      0,.TTY_ACSV
         LX      0,1                            CONSOLE HAS DATA
         ST      0,.TTY_STAT
         L       0,.TTY_ACSV
         RFI     0

TTY_ACSV DW      0

DEADBEEF DW      3735928559

CRLF     DW      2
         ASCII   \015\012

REGNAMES ASCII   \000\000AC\040
         ASCII   \040\040MQ\040
         ASCII   \040\040XY\040
         ASCII   \015\012X0\040
         ASCII   \040\040X1\040
         ASCII   \040\040X2\040
         ASCII   \040\040X3\040
         ASCII   \015\012X4\040
         ASCII   \040\040X5\040
         ASCII   \040\040X6\040
         ASCII   \040\040X7\040
         ASCII   \015\012AP\040
         ASCII   \040\040LR\040
         ASCII   \040\040SP\040
         ASCII   \015\012TI\040
         ASCII   \040\040TR\040

EXCNAMES ASCII   USER
         ASCII   INST
         ASCII   MEMX
         ASCII   DEVX
         ASCII   PPFR
         ASCII   PPFW
         ASCII   PPFS
         ASCII   TIME
         ASCII   DIVZ
         ASCII   NFPU
         ASCII   ????
         ASCII   ????
         ASCII   ????
         ASCII   ????
         ASCII   MCHK
         ASCII   PWRX

EXC_PFIX DW      7
         ASCII   \015\012EXC #

PSL      DW      5
         ASCII   \040PSL\040

PROMPT   DW      4
         ASCII   MCL>

MEM_SIZE DW      0

        ****************************************************************
        * Fastcall: X0-X3 for first args, caller saved
        *

        ****************************************************************
        * DUMPNOW - print current register contents
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

DUMPNOW  USING   0-15

         LX      3,2(13)
         BSM     .DUMPREGS
         BRM

        ****************************************************************
        * DUMPREGS - print register contents
        *
        *       X0 -> input data address
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

DUMPREGS USING   7,8,9

         LA      3,7
         LX      8,.REGNAMES
         LX      9,-16

         AX      13,-6                          Num buf 3(13)
         LX      9
         ST      0(13)                          Pad buf 0(13)

DUMPLOOP L       0(8)
         ST      1-
         LX      5
         ST      1-
         IA      8,8
         LA      13,3
         BSM     .WRITELD
         AX      13,2

         LX      3,3(13)
         L       4,0(7)
         LX      5,16
         BSM     .NUM2LD

         LX      3,3(13)
         LX      4,0(13)
         LX      5,040
         BSM     .LEFT_PAD

         LX      3,0(13)
         BSM     .WRITELD

         IA      7,7
         IA      9,9,TRN
         B       .DUMPLOOP

         LX      3,.CRLF
         BSM     .WRITELD
         LX      3,.CRLF
         BSM     .WRITELD

         AX      13,6
         BRM

        ****************************************************************
        * LEFT_PAD - you already know
        *
        *       X0 -> input buffer address
        *       X1 -> output buffer address, must specify length
        *       X2 -> padding character
        *
        *       Return -1 on failure, 0 on success
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

LEFT_PAD DW      0

         L       0(4)                           Abort if input too big
         S       0(3)
         TACBN   1
         BRM

         TACZ                                   Copy now if no padding
         B       .CPYINPUT

         NA      0,0

WRITEPAD ISTC    5,4,7
         IA      0,0,TRN
         B       .WRITEPAD

CPYINPUT LN      0(3)                           Done if input length 0
         TACZ
         BRM

CPYILOOP ILC     5,3,7
         ISTC    5,4,7
         IA      0,0,TRN
         B       .CPYILOOP

         XA      0,0
         BRM

COPYFAIL SMAC    36
         BRM

        ****************************************************************
        * NUM2LD - convert number to string
        *
        *       X0 -> buffer address
        *       X1 -> value
        *       X2 -> base
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

NUM2LD   USING   1,2

         ST      5,1-                           Set up divisor at 5
         LA      13,5

         XA      0,0                            Initialize string
         ST      0,0(3)

         LA      4,0

DIV      DU      0(5)                           Grab digit and remainder
         ST      2,1-                           Push digit
         ITN     0(3)                           Increment string size
         LA      1,0,TRN                        Test if done
         B       .DIV                           Loop until zero

         LA      3,5
         LX      1,58

CONVERT  L       4,1+                           Pop digits into string
         AA      4,4,I(48)
         TRGE    4,1
         AA      4,4,I(7)
         ISTC    4,3,7
         ITNE    0,0(5)
         B       .CONVERT

         IA      13,13
         BRM

        ****************************************************************
        * READLD - read up to n characters from the support console
        *
        *       X0 -> buffer address
        *       X1 -> max chars
        *
        *       Returns length of input
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

READLD   USING   7,8

         LA      3,7                            7: buffer
         XA      0,0                            Clear count field
         ST      0,0(7)
         TRZ     7                              Don't read if count 0
         BRM
         NA      4,8                            8: max length
         
         ST      7,1-                           Push head of string
         
         B       .READLOOP

IDLELOOP LX      3,.TEST_RST
         LX      4,.TTY_STAT                    Wait for full buffer
         BSM     .IDLE                          or crlf

READLOOP RIO     48,0,2                         Get buffer count
         LA      0,0,NL,CC,M(28),TRZ
         B       .IDLELOOP                      Idle if no data
         
         ITN     @0(13)                         Increment length
         RIO     48,0,0                         Get a byte
         ISTC    0,7,7                          Save

         IA      8,8,TRN                        Is buffer full?
         SA      0,0,NL,I(10),TRZ               Or did we get LF?
         B       .READDONE

         B       .READLOOP

READDONE L       0,@1+                          Return length
         BRM

        ****************************************************************
        * WRITELD - write a length-data string to the support console
        *
        *       X0 -> buffer address
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

WRITELD  DW      0

         LN      4,0(3)
         TRZ     4
         BRM

WRT_LOOP TIOBZ   48
         B       .WRT_LOOP

         ILC     0,3,7
         WIOS    48,0,0
         
         IA      4,4,TRN
         B       .WRT_LOOP
         
         BRM

        ****************************************************************
        * IDLE - wait until we have something to do
        *
        *      X0 -> test subroutine
        *      X1, X2, X3 -> arguments to subroutine (optional, will not
        *                    be modified by IDLE)
        *      Return -> whatever the subroutine puts in AC
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

IDLE     USING   12

         STMSK   1-                             Save interrupt mask

DOIDLE   LMSK    .MASKALL                       Mask all interrupts

         BL      0(3)                           Idle test callback
         TACN
         B       .DONTIDLE

         MWAIT   0(13)                          Wait for interrupts
         B       .DOIDLE

DONTIDLE LMSK    1+                             Restore mask
         BRM

MASKALL  DW      0

        ****************************************************************
        * TEST_RST - if (X1) != 0 then AC := (X1), (X1) := 0; return AC
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

TEST_RST L       0,0(4)
         TACZ
         B       .DONT_RST

         ST      2,1-
         XA      2,2
         ST      2,0(4)
         L       2,1+

DONT_RST B       0(12)

        ****************************************************************
        * ENUMDONE - break out of memory count loop
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

ENUMDONE DW      0
         L       .OK
         ST      0(3)
         BRM

        ****************************************************************
        * XPASS - test that always passes
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

XPASS    DW      0
         XA      0,0
         BRM

        ****************************************************************
        * XFAIL - test that always fails
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

XFAIL    DW      0
         L       .OK
         BRM

        ****************************************************************
        * MATHTST0 - basic ALU math test
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

MATHTST0 USING   3,4

         XA      4,4
         L       3,.CMA_0
         
         LA      0,0,CC,TCN                     0: clear carry flag
         B       .MT0_FAIL
         IA      4,4
         
         LA      0,0,SC,TCZ                     1: set carry flag
         B       .MT0_FAIL
         IA      4,4
         
         LA      0,0,CMC,TCN                    2: flip carry flag
         B       .MT0_FAIL
         IA      4,4
         
         LA      0,0,CMC,TCZ                    3: flip carry flag
         B       .MT0_FAIL
         IA      4,4

         L       .CMA_0                         4: CMA(-1) == 0
         CMA     0,3,TRN
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_0                         5: NA(-1) == 1
         NA      0,3
         A       3,.CMA_0
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_0                         6: IA(-1) == 0
         IA      0,3,CC,TRN                        + carry set
         B       .MT0_FAIL
         TCZ
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_0                         7: IA(-1) == 0
         IA      0,3,TCN                           + carry invert
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_LO                        8: ACA(CMA_LO,CMA_LO)
         L       3,.CMA_LO                         == -1
         ACA     0,3
         IA      3,3,TRN
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_LO                        -> CMA_0 - CMA_LO
         L       3,.CMA_0                          == CMA_HI
         SA      0,3,CC,TCZ                        + carry set
         B       .MT0_FAIL                      9: correct carry
         IA      4,4                            A: correct value
         S       3,.CMA_HI
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_0                         -> CMA_0 - CMA_0
         L       3,.CMA_0                          == 0
         SA      0,3,CC,TCZ                        + carry set
         B       .MT0_FAIL                      B: correct carry
         IA      4,4                            C: correct value
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_0                         -> CMA_LO - CMA_0
         L       3,.CMA_LO                         == CMA_LO + 1
         SA      0,3,CC,TCN                        + carry clear
         B       .MT0_FAIL                      D: correct carry
         IA      4,4                            E: correct value
         A       3,.CMA_0
         S       3,.CMA_LO
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       .CMA_HI                        -> CMA_LO + CMA_HI + 1
         L       3,.CMA_LO                         == 0
         IA      3,3                               + carry set
         AA      0,3,CC,TCZ                     F: correct carry
         B       .MT0_FAIL                      10: correct value
         IA      4,4
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       3,.CMA_0                       11: AND all together
         L       .CMA_LO
         L       2,.CMA_HI
         ANA     0,3
         ANA     2,3,TRN
         B       .MT0_FAIL
         IA      4,4
         
         L       3,.CMA_0                       12: (X)OR all together
         L       .CMA_LO
         L       2,.CMA_HI
         OA      0,2
         XA      0,3
         S       3,.CMA_HI
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       3,.CMA_0                       -> mask clear left
         LA      3,3,CC,M(18),TCN               13: correct carry
         B       .MT0_FAIL                      14: correct value
         IA      4,4
         S       3,.CMA_LO
         TRN     3
         B       .MT0_FAIL
         IA      4,4

         L       3,.CMA_LO                      -> mask set left
         LA      3,3,SC,M(18),TCZ               15: correct carry
         B       .MT0_FAIL                      16: correct value
         IA      4,4
         S       3,.CMA_0
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       3,.CMA_0                       -> mask clear right
         LA      3,3,CC,MR(18),TCN              17: correct carry
         B       .MT0_FAIL                      18: correct value
         IA      4,4
         S       3,.CMA_HI
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       3,.CMA_HI                      -> mask set right
         LA      3,3,SC,MR(18),TCZ              19: correct carry
         B       .MT0_FAIL                      1A: correct value
         IA      4,4
         S       3,.CMA_0
         TRN     3
         B       .MT0_FAIL
         IA      4,4
         
         L       3,.CMA_HI                      1B: rotate left
         LA      3,3,SC,R(18),M(18)
         S       3,.CMA_0
         TRN     3
         B       .MT0_FAIL
         IA      4,4

         L       3,.CMA_LO                      -> rotate carry
         LA      3,3,CC,R(19),MR(1)             1C: correct carry
         LA      3,3,SC,RC(18),TCN              1D: correct value
         B       .MT0_FAIL
         IA      4,4
         A       3,.CMA_HI
         S       3,.CMA_0
         TRN     3
         B       .MT0_FAIL
         IA      4,4

         XA      0,0
         BRM
         
MT0_FAIL LA      4,0
         BRM

CMA_0    DW      0777777777777
CMA_LO   DW      0777777
CMA_HI   DW      0777777000000

        ****************************************************************

START    LX      13,STACK

        ****************************************************************
        * Set up memory count. Also consider this a self-test of the
        * console, memory-not-present exception, BSM call and increment
        * indirect addressing features
        *

         LX      3,.EXTAB
         LX      4,.ENUMDONE
         ST      4,2(3)
         
         LX      3,.BANNER1
         BSM     .WRITELD
         
         LX      5,STACK
         O       5,.INCPTR
         ST      5,1-

MEMTEST  XA      0,0
         L       2,@0(13)
         TACZ
         B       .MEMTEST
         
         LX      3,.EXTAB
         LX      4,0
         ST      4,2(3)
         
         L       1+
         LA      0,4,CC,M(9)
         ST      4,.MEM_SIZE
         
         AX      13,-4
         LX      15
         
         LX      3,0(13)
         LX      5,10
         BSM     .NUM2LD
         
         LX      3,0(13)
         BSM     .WRITELD
         LX      3,.BANNER2
         BSM     .WRITELD
         
        ****************************************************************
        * Start executing the test battery
        *
        
         LX      .BATTERY
         O       .INCPTR
         ST      1-
         
         AX      13,-8
         LX      8
         ST      0(13)
         
        ****************************************************************
        * Left-pad buffer    0(13)
        * Number buffer      4(13)
        * Battery index      8(13), R8
        *
         
         XA      8,8
         
BATT_RUN L       7,@8(13)
         TRZ     7
         B       .BATTDONE
         
         LX      3,4(13)
         LA      8,4
         LX      5,10
         BSM     .NUM2LD
         
         IA      8,8
         
         LX      3,4(13)
         LX      4,0(13)
         LX      5,040
         BSM     .LEFT_PAD
         
         LX      3,0(13)
         BSM     .WRITELD
         
         LX      3,.TEST_TAB
         BSM     .WRITELD
         
         BSM     0(7)
         
         TACN
         B       .TESTFAIL
         
         LX      3,.PASS
         BSM     .WRITELD
         
         B       .BATT_RUN

TESTFAIL LA      0,7
         LX      3,.FAIL
         BSM     .WRITELD
         
         LX      9
         ST      0(13)
         
         LX      3,4(13)
         LA      7,4
         LX      5,16
         BSM     .NUM2LD
         
         LX      3,4(13)
         LX      4,0(13)
         LX      5,060
         BSM     .LEFT_PAD
         
         LX      3,0(13)
         BSM     .WRITELD
         
         LX      3,.CRLF
         BSM     .WRITELD
         
         LX      8
         ST      0(13)
         
         B       .BATT_RUN

BATTDONE AX      13,9
         LX      3,.CRLF
         BSM     .WRITELD

        ****************************************************************
        * Self-test complete, start the MCL interpreter (TODO)
        *
         
         AX      13,4

         RIO     48,0,1                         Enable keyboard
         AX      0,256
         WIO     48,0,1

         AX      13,-35

LOOP     LX      3,.PROMPT
         BSM     .WRITELD
         
         LA      13,3
         LX      4,170
         BSM     .READLD
         
         SA      0,0,I(2),NL,TRZ
         B       .DIE
         
         LA      13,3
         BSM     .WRITELD
         
         B       .LOOP

DIE      AX      13,35

         RIO     48,0,1                         Disable keyboard
         AX      0,-256
         WIO     48,0,1
         
         L       .OK
         B       -1

OK       DW      0631463146314
ZERO     DW      0
INCPTR   DW      0401000000000
BANNER1  DW      11
         ASCII   RDC-700/E1\040
BANNER2  DW      12
         ASCII   W ONLINE\015\012\015\012

PASS     DW      6
         ASCII   PASS\015\012
FAIL     DW      8
         ASCII   FAIL\040\040\040\040
TEST_TAB DW      8
         ASCII   ...\040\040\040\040\040

BATTERY  DW      XPASS,XFAIL,MATHTST0,0

        ****************************************************************

         ORIGIN  4096

STACK    BSS     0

         ORIGIN  8192

        ****************************************************************
        * String functions for 7-bit ASCII processing
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT
        
         ORIGIN  1024

        ****************************************************************
        * STRLEN - Length of zero-terminated string
        *
        * AP + 00 -> Byte pointer one before start of string
        *  Return -> Length of 7-bit zero-terminated string
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

STRLEN   USING   2,3

         SMAC    36                             A0: counter
         L       3,0(11)                        A3: byte pointer

STRL0    ILC     2,3,7                          Get character
         IA      0,0                            Count up starting at -1
         TRN     2                              Stop if zero
         B       .STRL0

         BRM

        ****************************************************************
        * STRMOVN - Copy up to n characters of zero-terminated string
        *           Will always write terminating zero
        *
        * AP + 00 -> Byte pointer one before start of source
        *    + 01 -> Byte pointer one before start of destination
        *    + 02 -> Maximum length to copy (not including zero)
        *  Return -> Remaining characters
        *  Update -> Source and destination indices
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

STRMOVN  USING   2,3,4

         LCM     2,2(11)                        A2: loop counter
         L       3,0(11)                        A3: source index
         L       4,1(11)                        A4: destination index
         
STRL1    ILC     0,3,7                          Get character
         TRN     0                              Break if ch = 0
         IA      2,2,TRZ                        or if count exhausted
         B       .STRL2
         
         ISTC    0,4,7                          Write copied character
         B       .STRL1                         Continue

STRL2    ST      3,0(11)                        Update source index
         ST      4,1(11)                        Update destination index
         CMA     2,0                            Return remaining chars
         XA      2,2
         ISTC    4,2,7                          Deposit zero
         BRM

        ****************************************************************
        * STRSETN - Repeat a character n times
        *
        * AP + 00 -> Byte pointer one before start of destination
        *    + 01 -> Length to write
        *    + 02 -> Character to write
        *  Return -> Updated index
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

STRSETN  USING   2,3

         LCM     2,1(11)                        A2: loop counter
         L       0,0(11)                        A0: destination index
         L       3,2(11)                        A3: character to write
         
STRL3    IA      2,2,TRZ                        Break if count exhausted
         B       .STRL4
         ISTC    3,0,7
         B       .STRL3

STRL4    BRM

        ****************************************************************
        * STRCMPN - Compare up to n characters of zero-terminated string
        *
        * AP + 00 -> Byte pointer one before start of A string
        *    + 01 -> Byte pointer one before start of B string
        *    + 02 -> Maximum length to compare
        *  Return -> 0 if equal, -1 otherwise
        *  Update -> Source and destination indices
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

STRCMPN  USING   2,3,4,5

         LCM     2,2(11)                        A2: loop counter
         L       3,0(11)                        A3: A index
         L       4,1(11)                        A4: B index
         
STRL5    ILC     0,3,7                          Get character A
         ILC     5,4,7                          Get character B
         OA      0,5,NL,TRN                     Succeed if A and B are 0
         IA      2,2,TRZ                        or if count exhausted
         B       .STRL6
         
         TREQ    5,0                            Keep going if A = B
         B       .STRL5
         
         LA      0,0,SC,M(36),TNV               Fail

STRL6    XA      0,0                            Success
         ST      3,0(11)                        Update A index
         ST      4,1(11)                        Update B index
         BRM

        ****************************************************************
        * STRINDC - Index of character in string
        *
        * AP + 00 -> Byte pointer one before start of string
        *    + 01 -> Character to seek
        *  Return -> Index of character (-1 if not found)
        *
        *INSTR.  ARGUMENTS                      REMARKS                 SORT

STRLEN   USING   2,3,4

         SMAC    36                             A0: counter
         L       3,0(11)                        A3: byte pointer
         L       4,1(11)                        A4: target

STRL7    ILC     2,3,7                          Get character
         IA      0,0                            Count up starting at -1
         TRNE    2,4                            Stop if equal to target
         TRN     2                              Or if character is zero
         B       .STRL7

         TACZ
         SMAC    36                             Return -1 if not found
         BRM

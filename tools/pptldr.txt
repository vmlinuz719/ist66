        ****************************************************************
        * SYSTEM-700 PAPER TAPE BOOTSTRAP PROGRAM
        *
        * READS A SERIES OF TAPE RECORDS STARTING WITH AN OCTAL ADDRESS
        * FOLLOWED BY A SEQUENCE OF PRINTABLE ASCII CHARACTERS 040-137
        * CORRESPONDING TO OCTAL NUMBERS 00-77 AND TERMINATED BY A | OR
        * ~ CHARACTER. ~ TELLS THE PROGRAM TO STOP READING THE TAPE AND
        * PAUSE EXECUTION, | TELLS IT TO CONTINUE READING THE NEXT
        * RECORD.
        *

        *INSTR.  ARGUMENTS                      REMARKS                 SORT
        
         ORIGIN  512

PPT_INIT NIOS    10

START    XA      3,3
         XA      2,2
         
GET_ADDR TIOND   10
         B       .GET_ADDR
         
         RIOS    10,0,0
         TACZ
         B       .GET_ADDR

         CMAC    33
         R       3,3
         OA      0,3
         ITNE    2,.NINE
         B       .GET_ADDR
         
         LX      1,-1(3)
         
GET_DATA TIOND   10
         B       .GET_DATA
         
         RIOS    10,3,0
         TRZ     3
         B       .GET_DATA
         
         LX      2,-042(3)
         ITNE    2,.PIPEMN1
         TNV
         B       .START
         ITNE    2,.TILDE
         TNV
         HLT
         
         ISTC    2,1,6
         B       .GET_DATA

NINE     DW      9
PIPEMN1  DW      91
TILDE    DW      94
